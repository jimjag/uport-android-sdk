// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    ext {

        kotlin_version = '1.2.61'
        kotlin_serialization_version = '0.6.1'
        coroutines_version = "0.26.1"

        android_tools_version = '3.3.0-alpha12'

        build_tools_version = "28.0.2"

        min_sdk_version = 19
        compile_sdk_version = 27
        target_sdk_version = compile_sdk_version

        test_runner_version = "1.0.2"
        test_rules_version = test_runner_version
        support_lib_version = "27.1.1"
        play_services_version = "15.0.0"
        espresso_version = "3.0.2"
        junit_version = "4.12"
        mockito_version = "2.19.0"
        mockito_kotlin_version = "2.0.0-RC1"

        moshi_version = "1.6.0"
        okhttp_version = "3.10.0"

        bivrost_version = "1c0efeb7f3"//"v0.6.2"
        kmnid_version = "0.2.1"
        kethereum_version = "0.53"
        khex_version = "0.5"

        uport_sdk_version = "0.2.2"
    }

    repositories {
        google()
        jcenter()
        maven { url "https://jitpack.io" }
        maven { url "https://kotlin.bintray.com/kotlinx" }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$android_tools_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlinx:kotlinx-gradle-serialization-plugin:$kotlin_serialization_version"
        classpath "com.github.gnosis.bivrost-kotlin:bivrost-gradle-plugin:$bivrost_version"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
        maven { url "https://kotlin.bintray.com/kotlinx" }
        mavenLocal()
    }
}

subprojects { subproject ->
    afterEvaluate {
        if (subproject.plugins.hasPlugin('maven')) {

            group "me.uport.sdk"
            version uport_sdk_version

            uploadArchives {
                repositories {
                    mavenDeployer {
                        if (project.hasProperty("MAVEN_REPOSITORY")) {
                            //nop
                        } else {
                            repository(url: mavenLocal().url)
                        }
                    }
                }
            }

        }

        if (subproject.plugins.hasPlugin("com.android.application") || subproject.plugins.hasPlugin("com.android.library") ) {
            subproject.android.packagingOptions.exclude("META-INF/main.kotlin_module")
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
